1. *Aliaksandr, Konan*
2. Minsk, **Belarus**
3. Hello, my name is Aliaksandr Konan. My goal is to study JS. I like to solve technical problems. I set goals for myself and put the maximum amount of effort to achieve of it. Solving complex problems, continuous self-development gives me pleasure and allows me to find self-realization in life
4. 1C Development. C, Asm for embedded systems
5. ```
	Процедура ОбработкаПроведения(Отказ, Режим)

		МетодСписания = РегистрыСведений.УчетнаяПолитика.ПолучитьПоследнее(Дата).МетодСписания;
	
		Если МетодСписания.Пустая() Тогда
		 	Сообщение = Новый СообщениеПользователю;
		 	Сообщение.Текст = "Не выбрана учетная политика предприятия";
		 	Сообщение.Сообщить();
		 	Отказ = Истина;
		 	Возврат;		
		КонецЕсли;
	
		СортировкаПартий = ?(МетодСписания = Перечисления.МетодыСписания.ФИФО, "", " УБЫВ");
	
		Движения.Остатки.Записывать = Истина;
    		Движения.Продажи.Записывать = Истина;
	
		Запрос = Новый Запрос;
	
		Запрос.Текст = "ВЫБРАТЬ
	               |	МИНИМУМ(РасходнаяНакладнаяСписокТоваров.НомерСтроки) КАК НомерСтроки,
	               |	РасходнаяНакладнаяСписокТоваров.Номенклатура,
	               |	СУММА(РасходнаяНакладнаяСписокТоваров.Количество) КАК Количество,
	               |	СУММА(РасходнаяНакладнаяСписокТоваров.Сумма) КАК Выручка
	               |ПОМЕСТИТЬ ДокТч
	               |ИЗ
	               |	Документ.РасходнаяНакладная.СписокТоваров КАК РасходнаяНакладнаяСписокТоваров
	               |ГДЕ
	               |	РасходнаяНакладнаяСписокТоваров.Ссылка = &Ссылка
	               |	И РасходнаяНакладнаяСписокТоваров.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Товар)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	РасходнаяНакладнаяСписокТоваров.Номенклатура
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ДокТч.НомерСтроки КАК НомерСтроки,
	               |	ДокТч.Номенклатура КАК Номенклатура,
	               |	ДокТч.Количество КАК Количество,
	               |	ДокТч.Выручка КАК Выручка,
	               |	ОстаткиОстатки.Партия КАК Партия,
	               |	ЕСТЬNULL(ОстаткиОстатки.КоличествоОстаток, 0) КАК КоличествоВсего,
	               |	ЕСТЬNULL(ОстаткиОстатки.СтоимостьОстаток, 0) КАК СтоимостьВсего,
	               |	ЕСТЬNULL(ОстаткиОстатки1.КоличествоОстаток, 0) КАК КоличествоВсегоОбщее
	               |ИЗ
	               |	ДокТч КАК ДокТч
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Остатки.Остатки(
	               |				&МоментВремени,
	               |				Номенклатура В
	               |					(ВЫБРАТЬ
	               |						ДокТч.Номенклатура
	               |					ИЗ
	               |						ДокТч КАК ДокТч)) КАК ОстаткиОстатки
	               |		ПО ДокТч.Номенклатура = ОстаткиОстатки.Номенклатура
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.Остатки.Остатки КАК ОстаткиОстатки1
	               |		ПО ДокТч.Номенклатура = ОстаткиОстатки1.Номенклатура
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ОстаткиОстатки.Партия.Дата"+СортировкаПартий+"
	               |ИТОГИ
	               |	МИНИМУМ(НомерСтроки),
	               |	МИНИМУМ(Количество),
	               |	МИНИМУМ(Выручка),
	               |	МИНИМУМ(КоличествоВсегоОбщее)
	               |ПО
	               |	Номенклатура";
				   
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Запрос.УстановитьПараметр("МоментВремени",МоментВремени());
		Результат = Запрос.Выполнить();
		
	    	Выборка = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.Количество > Выборка.КоличествоВсегоОбщее Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Не хватает товара " + Выборка.Номенклатура +" в количестве " + (Выборка.Количество - Выборка.КоличествоВсегоОбщее);
				Сообщение.Поле = "СписокТоваров["+ (Выборка.НомерСтроки-1) + "].Количество";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();        
				 
				Отказ = Истина;
				Движения.Остатки.Записывать = Ложь;	
				Движения.Продажи.Записывать = Ложь;
			КонецЕсли;			
		КонецЦикла;      
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Выборка.Сбросить();
		
		Пока Выборка.Следующий()  Цикл
			 ОсталосьСписать = Выборка.Количество;
			 СебестоимостьВсего = 0;
			 
			 ВыборкаПартии = Выборка.Выбрать();
			 Пока  ВыборкаПартии.Следующий() И ОсталосьСписать <>0  Цикл
				 Списать = МИН(ОсталосьСписать,ВыборкаПартии.КоличествоВсего);
				 Себестоимость = (Списать/ВыборкаПартии.КоличествоВсего)*ВыборкаПартии.СтоимостьВсего;
				 
				 Движение = Движения.Остатки.Добавить();
				 Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				 Движение.Период = Дата;
				 Движение.Партия = ВыборкаПартии.Партия;
				 Движение.Номенклатура = Выборка.Номенклатура;
				 Движение.Количество = Списать;
				 Движение.Стоимость = Себестоимость; 
				 
				 СебестоимостьВсего = СебестоимостьВсего + Себестоимость;
				 
				 ОсталосьСписать = ОсталосьСписать - Списать;		 
			 КонецЦикла;
			  
			 //Движение по продажам товара
			 Движение = Движения.Продажи.Добавить();
			 Движение.Период = Дата;
			 Движение.Номенклатура = Выборка.Номенклатура;
			 Движение.Количество = Выборка.Количество;
			 Движение.Себестомость = СебестоимостьВсего;
			 Движение.Выручка = Выборка.Выручка;		 
		 КонецЦикла;
		 
		 /////////////////////////////////////////////////////////////////////////////
		 // Дополнительно для движений  по продажам услуг 
		 
		 Запрос = Новый Запрос;
		 Запрос.Текст = "ВЫБРАТЬ
						|	РасходнаяНакладнаяСписокТоваров.Номенклатура,
						|	СУММА(РасходнаяНакладнаяСписокТоваров.Количество) КАК Количество,
						|	СУММА(РасходнаяНакладнаяСписокТоваров.Сумма) КАК Выручка
						|ИЗ
						|	Документ.РасходнаяНакладная.СписокТоваров КАК РасходнаяНакладнаяСписокТоваров
						|ГДЕ
						|	РасходнаяНакладнаяСписокТоваров.Ссылка = &Ссылка
						|	И РасходнаяНакладнаяСписокТоваров.Номенклатура.ВидНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ВидыНоменклатуры.Услуга)
						|
						|СГРУППИРОВАТЬ ПО
						|	РасходнаяНакладнаяСписокТоваров.Номенклатура";
						
		Запрос.УстановитьПараметр("Ссылка",Ссылка);
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Движение = Движения.Продажи.Добавить();
			Движение.Период = Дата;
			Движение.Номенклатура = Выборка.Номенклатура;
			Движение.Количество = Выборка.Количество;
			Движение.Себестомость = 0;
			Движение.Выручка = Выборка.Выручка;		
			
		КонецЦикла;
		
	КонецПроцедуры
   ```